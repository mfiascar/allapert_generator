//******************************************
// IP1 & 5 AT COLLISION WITH HL-LHC VALUES
//******************************************
option, warn, info; 

//-----------------------------------------
// Shortcut creation for needed directories
//-----------------------------------------
system, "ln -fns /afs/cern.ch/eng/lhc/optics/V6.503 db5";                    // Nominal
system, "ln -fns /afs/cern.ch/eng/lhc/optics/HLLHCV1.0 hl";                  // High Luminosity
system, "ln -fns /afs/cern.ch/eng/lhc/optics/HLLHCV1.1 slhc";                // Newest version
system, "ln -fns /afs/cern.ch/user/a/ansantam/public/toolkit tkit";          // Beam parameters
system, "ln -fns /afs/cern.ch/user/a/ansantam/public/MADX/collimation coll"; // Collimation

//---------------------------------
// Load ATS macros and select beam
//--------------------------------- 
call, file = "slhc/toolkit/macro.madx";
// on_disp = 1;  // parasitic dispersion
mylhcbeam = 1;
// mylhcbeam = 4; // For Beam 4, in the other sense

system, "cp slhc/toolkit/macro.madx .";
//--------------
// Verbose off
//--------------
option, -echo, -warn, -info;

//---------------------------------
// Redefinition of elements' length
//---------------------------------
REAL CONST l.TAN   = 3.7;
REAL CONST l.TANAL = l.TAN;
REAL CONST l.TANAR = l.TAN;
REAL CONST l.TANC  = l.TAN;
REAL CONST l.TCT   = 1.0;
REAL CONST l.TCTH  = l.TCT;
REAL CONST l.TCTVA = l.TCT;

//--------------------------------------------
// Load sequence files (position of elements)
//--------------------------------------------
// call, file = "db5/V6.5.seq";                 !+=+=+= fatal: negative drift between elements  atlaspipe5.r1:1 and mbas2.1r1:1, length -1.478670e+00
// call, file = "slhc/hllhc_sequence.madx";     !+=+=+= fatal: unknown class type: mqy
// call, file = "hllhc.seq"                     !+=+=+= fatal: c6t - no current sequence.
// call, file = "hllhc_sequence2.madx"          !+=+=+= fatal: c6t - no current sequence.

if (mylhcbeam>2){
  call,file="slhc/hllhc_thinb4.seq";
} else {
  call,file="slhc/hllhc_thin.seq";
};

system, "cp slhc/hllhc_thin.seq .";

//--------------------
// Load strength files
//--------------------
call, file = "slhc/opt_round_thin.madx";  // beta*=15 cm, round optics

//------------------------------
// Load and define the aperture
//-----------------------------
call, file = "db5/aperture/aperture.b1.madx";
call, file = "db5/aperture/aper_tol.b1.madx";
call, file = "slhc/aperture/aperture_upgrade_MS.madx";
call, file = "slhc/aperture/aperture_upgrade_IT.madx";

// Not updated -- !Check where we can get the info?
//------------------------------------------------
call, file = "/afs/cern.ch/eng/lhc/optics/V6.503/aperture/as-built/layoutapertures.madx";            // Vacuum markers
call, file = "/afs/cern.ch/eng/lhc/optics/V6.503/aperture/as-built/exp_pipe_model_after_LS1.madx";   // Apertures around IPs
call, file = "/afs/cern.ch/eng/lhc/optics/V6.503/aperture/as-built/exp_pipe_install_after_LS1.madx"; // Apertures around IPs

//--------------------------------
// Define the beam for the machine
//--------------------------------
exec, crossing_enable;
exec, mk_beam(7000); // Collision

//--------------------------------
// Define the crossings at the IPS
//--------------------------------
on_x1 := 1; on_sep1 := 0; on_sol_atlas := 1;
on_x2 := 1; on_sep2 := 0; on_alice:= 1; on_sol_alice := 1;
on_x5 := 1; on_sep5 := 0; on_sol_cms := 1;
on_x8 := 1; on_sep8 := 0; on_lhcb:= 1;
on_disp := 1;

//------------------------
// Activate crab cavities
//------------------------
on_crab1 := 1; on_crab5 := 1; z_crab := 0.075;

//------------------------------------------------------
// Load the beam parameters (to obtain sigmas as output)
//------------------------------------------------------
call, file = "tkit/Beam_7_TeV.madx";  // Important to call these files first, if not Beam.madx doesnt work
// call, file = "tkit/Beam_450_GeV.madx";
call, file = "tkit/Beam.madx";   

//---------------------------------------------------------------------------------------------------
// IP1
//---------------------------------------------------------------------------------------------------
//-------
// Beam 1
//-------
use, sequence = lhcb1;
select, flag = twiss, clear;
select, flag = twiss,
column = name, s, l, x, y, aper_1, aper_2, aper_3, aper_4, alfx, alfy, px, py, sigxd, sigyd, betx, bety, sigx, ex, sigy, ey, dx, dpx, dy, dpy, wx, wy, T, PT, DELTAP, beta;
twiss, table = twiss, file = "twiss_ip1_b1.tfs";

select, flag = aperture, clear;
select, flag = aperture, column = keyword, name, parent, s, l, aper_1, aper_2, aper_3, aper_4;
aperture, file = "allapert_twiss.b1";

stop;




